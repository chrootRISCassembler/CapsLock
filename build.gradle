/*
    Copyright (C) 2018 RISCassembler

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as
    published by the Free Software Foundation, either version 3 of the
    License, or (at your option) any later version.
    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.
    You should have received a copy of the GNU Affero General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

import java.awt.Desktop
import java.nio.file.Files
import java.nio.file.LinkOption
import java.nio.file.Paths
import java.nio.file.StandardCopyOption
import java.time.LocalDateTime

group 'CapsLock'
version '3.0.0-SNAPSHOT'

apply plugin: 'java'
sourceCompatibility = 10
targetCompatibility = 10

apply plugin: "groovy"
compileGroovy.enabled = false

apply plugin: 'application'
mainClassName = 'capslock.capslock.main.CapsLock'
startScripts.enabled = false
distZip.enabled = false
distTar.enabled = false

apply plugin: 'com.github.johnrengelman.shadow'
startShadowScripts.enabled = false
shadowDistTar.enabled = false
shadowDistZip.enabled = false

repositories {
    mavenCentral()
    jcenter()

    flatDir{
        dirs 'lib'
    }
}

class PrivateLibDownload{
    private static final String GitHubRepository = 'https://github.com/chrootRISCassembler/dependence/raw/master/'
    String libName
    String libVersion
    String jarName(){libName + '-' + libVersion + '.jar'}
    def localJarPath(){Paths.get(System.getProperty("user.dir") + "/lib/" + jarName())}

    void downloadLibJar(){
        try {
            URL url = new URL(GitHubRepository + jarName())
            InputStream inStream = url.openStream()
            Files.copy(inStream, localJarPath(), StandardCopyOption.REPLACE_EXISTING)
            println "Download " + libName +  " succeeded"
        } catch (Exception ex) {
            println "Download " + libName + " failed"
            throw ex
        }
    }

    void downloadIfNotExist(){
        if(!Files.exists(localJarPath(), LinkOption.NOFOLLOW_LINKS))downloadLibJar()
    }
}

final fileChecker = new PrivateLibDownload()
fileChecker.libName = "FileChecker"
fileChecker.libVersion = "1.1.1-SNAPSHOT"

final trivialLogger = new PrivateLibDownload()
trivialLogger.libName = "TrivialLogger"
trivialLogger.libVersion = "1.1.1-SNAPSHOT"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

dependencies {
    compile group: 'org.json', name: 'json', version:  '+'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile group: "org.testfx", name: "testfx-core", version: "4.0.13-alpha"
    testCompile group: 'org.testfx', name: 'testfx-spock', version: '4.0.13-alpha'

    compile name: fileChecker.libName, version: fileChecker.libVersion
    compile name: trivialLogger.libName, version: trivialLogger.libVersion
}

tasks.withType(JavaCompile){
    options.encoding = 'UTF-8'
}

processResources{
    exclude 'MainForm.*'
    doLast {
        def buildInfoFile = new File(processResources.destinationDir.path + '/BuildInfo.txt')
        buildInfoFile.bytes = []
        buildInfoFile << 'version : ' + version + '\n'
        buildInfoFile << 'git_commit_hash : ' +  'git rev-parse HEAD'.execute().text.trim()  + '\n'
        buildInfoFile << 'date : ' + LocalDateTime.now()
    }
}

jar{
    manifest{
        attributes 'Main-Class': mainClassName
    }
    println version
}

task cpResources(type : Copy){
    from 'src/main/resources'
    into 'build/classes/java/main/capslock/capslock/main'
}

run {
    dependsOn cpResources
    println("Current directory is " + workingDir)
    workingDir = '../TestEnv'
    println("cd to the testing environment : " + workingDir)
}

compileJava.doFirst {
    def libDir = Paths.get(System.getProperty("user.dir") + "/lib/")
    if (Files.notExists(libDir)) {
        Files.createDirectory(libDir)
    }

    fileChecker.downloadIfNotExist()
    trivialLogger.downloadIfNotExist()
}

task forceUpdateLib{
    doLast {
        def libDir = Paths.get(System.getProperty("user.dir") + "/lib/")
        if (Files.notExists(libDir)) {
            Files.createDirectory(libDir)
        }
        fileChecker.downloadLibJar()
        trivialLogger.downloadLibJar()
    }
}

task game_infoPackageJar(type: Jar) {
    from(sourceSets.main.output) {
        include 'capslock/game_info/**'
    }
    version = '2.0.0-SNAPSHOT'
    baseName = 'game_info'
}

task game_infoJavadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    baseName = 'game_info'
    from javadoc.destinationDir
    version = '2.0.0-SNAPSHOT'
}

javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
    options.links "https://docs.oracle.com/javase/10/docs/api/"

    doLast{
        //デフォルトブラウザで生成したjavadocを自動で開く
        def userDir = System.getProperty("user.dir")
        def indexURI = new URI(userDir.replace('\\', '/') + "/build/docs/javadoc/index.html")
        Desktop.getDesktop().browse(indexURI)
    }
}

artifacts {
    archives game_infoPackageJar
    archives game_infoJavadocJar
}